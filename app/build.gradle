apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "bg.o.sim.annawave"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "[alpha] Arsenic" /* Similarly to this application, Arsenic is one of the most toxic elements that can be found ~
                                            ~ Read more: https://www.lenntech.com/periodic/elements/as.htm#Health%20effects%20of%20arsenic#ixzz5aHB12OCA*/
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Mocking BuildConfig field default values
        buildConfigField "boolean", "IS_NET_MOCK", "false"
        buildConfigField "boolean", "IS_DAO_MOCK", "false"

    }
    buildTypes {
        debug {
            buildConfigField "boolean", "IS_MOCK", "true"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "module", "isMock"


    productFlavors {
        ext {
            configField = []
        }

        net {
            dimension "module"
            configField.add "IS_NET_MOCK"
        }
        dao {
            dimension "module"
            configField.add "IS_DAO_MOCK"
        }

        allMods {
            dimension "module"
            configField.add "IS_NET_MOCK"
            configField.add "IS_DAO_MOCK"
        }

        standard {
            dimension "isMock"
            configField.each { buildConfigField "boolean", it, "false" }
        }
        mock {
            dimension "isMock"
            configField.each { buildConfigField "boolean", it, "true" }
        }
    }

}

dependencies {
    // Local libs, deps and dirs
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin eXtentions for Android
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.0.1'

    // AndroidX support libs
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Android Architecture components
    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0-beta02"
    implementation "android.arch.navigation:navigation-ui-ktx:1.0.0-beta02"

    // UI and design
    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'com.google.android:flexbox:1.1.0'

    // networking
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.5.0'
    // networking: for development only [TODO - exclude in release builds?]
    implementation 'com.squareup.retrofit2:retrofit-mock:2.5.0'

    // Dagger2 - dependency injection
    implementation 'com.google.dagger:dagger-android:2.21'
    implementation 'com.google.dagger:dagger-android-support:2.21'
    kapt 'com.google.dagger:dagger-android-processor:2.21'
    kapt 'com.google.dagger:dagger-compiler:2.21'

    // deserialization
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.8'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8'

    // Testing [#some_day #defs_gon_happen]
    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
